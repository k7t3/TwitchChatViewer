plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'

    // 2024/3/29現在、Java 19までがサポートなので対応待ち
    id 'org.beryx.runtime' version '1.13.1'
}

group = 'io.github.k7t3'
version = '1.0.0'

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

compileJava {
    options.encoding = 'UTF-8'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

javafx {
    version = '22'
    modules = [ 'javafx.controls', 'javafx.fxml', 'javafx.web' ]
}

application {
    mainClass = 'com.github.k7t3.tcv.view.TCVApp'
    applicationName = 'Twitch Chat Viewer'
}

dependencies {

    // Logger
    implementation 'org.slf4j:slf4j-api:2.0.11'
    implementation 'org.slf4j:slf4j-jdk14:2.0.11'

    // Twitchクライアント
    implementation 'com.github.twitch4j:twitch4j:1.19.0'

    // キャッシュ
    implementation 'com.github.ben-manes.caffeine:caffeine:3.1.8'

    // AtlantaFXと同じアイコンパックを使用する
    implementation 'io.github.mkpaz:atlantafx-base:2.0.1'
    implementation 'org.kordamp.ikonli:ikonli-javafx:12.3.1'
    implementation 'org.kordamp.ikonli:ikonli-feather-pack:12.3.1'
    implementation 'org.kordamp.ikonli:ikonli-material2-pack:12.3.1'

    // アイコンパック
    implementation 'org.kordamp.ikonli:ikonli-fontawesome5-pack:12.3.1'

    // MVVM
    implementation "de.saxsys:mvvmfx:1.8.0"

    // チャットビュー用のVirtualFlow
    implementation 'org.fxmisc.flowless:flowless:0.7.2'

    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

/**
 * 2024/3/28 現在、org.beryx.runtimeプラグインのJavaの対応バージョンは19までなので使用不可。
 */
runtime {
    options = [ '--strip-debug', '--compress', 'zip-6', '--no-header-files', '--no-man-pages' ]

    launcher {
        noConsole = true
    }

    jpackage {
        imageName = 'twitch-chat-viewer'
        skipInstaller = true
        appVersion = version
    }
}

test {
    useJUnitPlatform()
}